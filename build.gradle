plugins {
    id "checkstyle"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "jacoco"
    id "java"
    id "pmd"
}

defaultTasks 'build'

jacoco {
    toolVersion = '0.8.4'
}

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    checkstyle {
        ignoreFailures = true
        toolVersion = '8.22'
    }

    dependencies {
        implementation(enforcedPlatform('org.junit:junit-bom:5.5.1'))
        implementation(enforcedPlatform('org.springframework.boot:spring-boot-dependencies:2.1.6.RELEASE'))
    }

    dependencyManagement {
        dependencies {
            dependency 'org.assertj:assertj-core:3.13.1'
        }
    }

    pmd {
        consoleOutput = true
        ignoreFailures = true
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
        ruleSets = []
        toolVersion = '6.15.0'
    }

    test {
        useJUnitPlatform()
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn = subprojects.build

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = false
        csv.enabled = false
    }
}

rootProject.check.dependsOn codeCoverageReport
