buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
    }
}

plugins {
    id "checkstyle"
    id "jacoco"
    id "java"
    id "pmd"
}

apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    jcenter()
}

dependencies {
    testCompile("org.junit.jupiter:junit-jupiter-api:5.1.0")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.1.0")
    testCompile("org.assertj:assertj-core:3.9.1")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'build'

jacoco {
    applyTo junitPlatformTest
    toolVersion = '0.8.0'
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

junitPlatformTest {
    jacoco {
        destinationFile = file("${buildDir}/jacoco/test.exec")
    }
}

check.dependsOn jacocoTestReport

checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    ignoreFailures = true
    toolVersion = '8.5'
}

pmd {
    consoleOutput = true
    ignoreFailures = true
    ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
    ruleSets = []
    toolVersion = '6.1.0'
}
