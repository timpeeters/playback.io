plugins {
    id "checkstyle"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "jacoco"
    id "java"
    id "pmd"
}

defaultTasks 'build'

jacoco {
    toolVersion = '0.8.1'
}

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    checkstyle {
        ignoreFailures = true
        toolVersion = '8.12'
    }

    dependencies {
        implementation 'org.junit:junit-bom:5.2.0'
        implementation 'org.springframework.boot:spring-boot-dependencies:2.0.4.RELEASE'
    }

    dependencyManagement {
        dependencies {
            dependency 'org.assertj:assertj-core:3.11.0'
        }
    }

    pmd {
        consoleOutput = true
        ignoreFailures = true
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
        ruleSets = []
        toolVersion = '6.6.0'
    }

    test {
        useJUnitPlatform()
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn = subprojects.build

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = false
        csv.enabled = false
    }
}

rootProject.check.dependsOn codeCoverageReport
