plugins {
    id "checkstyle"
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id "jacoco"
    id "java"
    id "pmd"
}

defaultTasks 'build'

jacoco {
    toolVersion = '0.8.1'
}

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    checkstyle {
        ignoreFailures = true
        toolVersion = '8.10'
    }

    dependencies {
        compile 'org.apache.commons:commons-lang3:3.6'

        implementation 'org.junit:junit-bom:5.2.0'
        implementation 'org.springframework.boot:spring-boot-dependencies:2.0.1.RELEASE'
    }

    dependencyManagement {
        dependencies {
            dependency 'org.assertj:assertj-core:3.9.1'
        }
    }

    pmd {
        consoleOutput = true
        ignoreFailures = true
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
        ruleSets = []
        toolVersion = '6.3.0'
    }

    test {
        useJUnitPlatform()
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.build

    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)

    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }

    onlyIf = {
        true
    }

    reports {
        html.enabled = false
        xml.enabled = true
        csv.enabled = false
    }
}

rootProject.check.dependsOn jacocoRootReport
