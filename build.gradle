plugins {
    id "checkstyle"
    id "jacoco"
    id "java"
    id "pmd"
}

defaultTasks 'build'

jacoco {
    toolVersion = '0.8.1'
}

jacocoTestReport {
    reports {
        html.enabled = false
        xml.enabled = true
    }
}

jacocoTestReport.dependsOn {
    subprojects*.test
}

build.dependsOn jacocoTestReport

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        ignoreFailures = true
        toolVersion = '8.9'
    }

    pmd {
        consoleOutput = true
        ignoreFailures = true
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
        ruleSets = []
        toolVersion = '6.2.0'
    }

    test {
        jacoco {
            append = true
            destinationFile = file("${rootProject.projectDir}/build/jacoco/test.exec")
        }

        useJUnitPlatform()
    }
}
